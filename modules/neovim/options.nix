{
  lib,
  flake-parts-lib,
  ...
}:
with lib; let
  inherit (builtins) isFunction typeOf;
  inherit (flake-parts-lib) mkPerSystemOption;

  mkLuaOption = type:
    mkOption {
      type = types.nullOr (types.oneOf [type (types.functionTo type)]);
      default = null;
    };

  toLuaHashMap = v: "{ ${concatStringsSep ", " (mapAttrsToList (name: value: "${name} = ${toLua value}") v)} }";

  toLuaArray = v: "{ ${concatMapStringsSep ", " toLua v} }";

  # TODO: Surely there is a better way?
  toLua = v:
    if (typeOf v) == "string"
    then ''"${v}"''
    else if (typeOf v) == "bool"
    then
      if v
      then "true"
      else "false"
    else if (typeOf v) == "set"
    then toLuaHashMap v
    else if (typeOf v) == "list"
    then toLuaArray v
    else if isFunction v
    then v {}
    else toString v;
in {
  options = {
    perSystem = mkPerSystemOption ({
      config,
      pkgs,
      ...
    }: {
      options = with lib.types; {
        neovim.build = {
          vimOptions = mkOption {
            internal = true;
            type = package;
          };
        };

        # TODO: Generate these.
        vim.opt = {
          autoindent = mkLuaOption bool;
          belloff = mkLuaOption str;
          breakindent = mkLuaOption bool;
          cindent = mkLuaOption bool;
          clipboard = mkLuaOption str;
          cmdheight = mkLuaOption int;
          conceallevel = mkLuaOption int;
          cursorline = mkLuaOption bool;
          equalalways = mkLuaOption bool;
          expandtab = mkLuaOption bool;
          exrc = mkLuaOption bool;
          fillchars = mkLuaOption attrs;
          foldenable = mkLuaOption bool;
          foldlevel = mkLuaOption int;
          formatoptions = mkLuaOption str;
          grepprg = mkLuaOption str;
          hidden = mkLuaOption bool;
          hlsearch = mkLuaOption bool;
          ignorecase = mkLuaOption bool;
          inccommand = mkLuaOption str;
          incsearch = mkLuaOption bool;
          keywordprg = mkLuaOption str;
          linebreak = mkLuaOption bool;
          listchars = mkLuaOption attrs;
          modelines = mkLuaOption int;
          mouse = mkLuaOption str;
          number = mkLuaOption bool;
          pumblend = mkLuaOption int;
          relativenumber = mkLuaOption bool;
          scrolloff = mkLuaOption int;
          secure = mkLuaOption bool;
          shada = mkLuaOption (listOf str);
          shiftwidth = mkLuaOption int;
          shortmess = mkLuaOption str;
          showbreak = mkLuaOption str;
          showcmd = mkLuaOption bool;
          showmatch = mkLuaOption bool;
          showmode = mkLuaOption bool;
          signcolumn = mkLuaOption str;
          smartcase = mkLuaOption bool;
          softtabstop = mkLuaOption int;
          splitbelow = mkLuaOption bool;
          splitright = mkLuaOption bool;
          swapfile = mkLuaOption bool;
          tabstop = mkLuaOption int;
          termguicolors = mkLuaOption bool;
          timeoutlen = mkLuaOption int;
          updatetime = mkLuaOption int;
          wildignore = mkLuaOption (listOf str);
          wildmode = mkLuaOption (listOf str);
          wildoptions = mkLuaOption str;
          wrap = mkLuaOption bool;
        };
      };

      config = {
        neovim.build.vimOptions = let
          opts =
            mapAttrsToList (
              name: value: ''
                vim.opt.${name} = ${toLua value}
              ''
            )
            (filterAttrs (_: value: value != null) config.vim.opt);

          mod = pkgs.writeText "options.lua" ''
            -- Generated by Nix.
            ${concatStringsSep "\n" opts}
          '';
        in
          # TODO: Either this...
          pkgs.runCommandLocal "options.lua" {} ''
            ln -s ${mod} $out
          '';
        # ... or this? What do we think is better?
        # pkgs.stdenvNoCC.mkDerivation {
        #   name = "neovim-options";

        #   dontUnpack = true;
        #   dontConfigure = true;
        #   dontBuild = true;

        #   installPhase = ''
        #     runHook preInstall
        #     mkdir -p $out/lua/neovim-nix
        #     ln -s ${mod} $out/lua/neovim-nix/options.lua
        #     runHook postInstall
        #   '';
        # };
      };
    });
  };
}
