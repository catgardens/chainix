{
  lib,
  flake-parts-lib,
  neovim-lib,
  ...
}: let
  inherit (lib) concatStringsSep filterAttrs mapAttrsToList mkOption;
  inherit (flake-parts-lib) mkPerSystemOption;
  inherit (neovim-lib) mkLuaOption toLua;
in {
  options = {
    perSystem = mkPerSystemOption ({
      config,
      pkgs,
      ...
    }: {
      options = with lib.types; {
        neovim.build = {
          vimGlobals = mkOption {
            internal = true;
            type = package;
          };
        };

        # TODO: Generate these.
        vim.g = {
          do_filetype_lua = mkLuaOption bool;
          loaded_2html_plugin = mkLuaOption bool;
          loaded_getscript = mkLuaOption bool;
          loaded_getscriptPlugin = mkLuaOption bool;
          loaded_gzip = mkLuaOption bool;
          loaded_logiPat = mkLuaOption bool;
          loaded_matchit = mkLuaOption bool;
          loaded_matchparen = mkLuaOption bool;
          loaded_netrw = mkLuaOption bool;
          loaded_netrwPlugin = mkLuaOption bool;
          loaded_netrwSettings = mkLuaOption bool;
          loaded_rrhelper = mkLuaOption bool;
          loaded_tar = mkLuaOption bool;
          loaded_tarPlugin = mkLuaOption bool;
          loaded_vimball = mkLuaOption bool;
          loaded_vimballPlugin = mkLuaOption bool;
          loaded_zip = mkLuaOption bool;
          loaded_zipPlugin = mkLuaOption bool;
          mapleader = mkLuaOption str;
        };
      };

      config = {
        neovim.build.vimGlobals = let
          opts =
            mapAttrsToList (
              name: value: ''
                vim.g.${name} = ${toLua value}
              ''
            )
            (filterAttrs (_: value: value != null) config.vim.g);

          mod = pkgs.writeText "globals.lua" ''
            -- Generated by Nix.
            ${concatStringsSep "\n" opts}
          '';
        in
          # TODO: Either this...
          pkgs.runCommandLocal "globals.lua" {} ''
            ln -s ${mod} $out
          '';
        # ... or this? What do we think is better?
        # pkgs.stdenvNoCC.mkDerivation {
        #   name = "neovim-options";

        #   dontUnpack = true;
        #   dontConfigure = true;
        #   dontBuild = true;

        #   installPhase = ''
        #     runHook preInstall
        #     mkdir -p $out/lua/neovim-nix
        #     ln -s ${mod} $out/lua/neovim-nix/options.lua
        #     runHook postInstall
        #   '';
        # };
      };
    });
  };
}
