{
  lib,
  flake-parts-lib,
  ...
}: let
  inherit (flake-parts-lib) mkPerSystemOption;
  inherit (lib) mkOption types;

  mkInitLua = {
    config,
    pkgs,
  }:
    pkgs.writeTextFile {
      name = "init.lua";
      text = ''
        -- Generated by Nix (via github:willruggiano/neovim.nix)
        vim.cmd.source "${config.neovim.build.before}"
        vim.cmd.source "${config.neovim.build.plugins}"
      '';
    };
in {
  options = {
    perSystem = mkPerSystemOption ({
      inputs',
      config,
      pkgs,
      ...
    }: {
      options = with types; {
        neovim = {
          package = mkOption {
            type = package;
            description = "The Neovim derivation to use";
            inherit (inputs'.neovim.packages) default;
          };

          src = mkOption {
            type = nullOr path;
            description = "The root directory of your dotfiles, to be added to 'runtimepath'";
            default = null;
          };

          build = {
            initlua = mkOption {
              internal = true;
              type = package;
            };

            before = mkOption {
              internal = true;
              type = package;
            };
          };
        };
      };

      config = {
        neovim.build.before = pkgs.writeTextFile {
          name = "before.lua";
          text = ''
            -- Generated by Nix (via github:willruggiano/neovim.nix)
          '';
        };

        neovim.build.initlua = mkInitLua {
          inherit config pkgs;
        };
      };
    });
  };
}
